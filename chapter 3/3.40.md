## exercise 3.40

### a) 

For the sake of description, label two processes.

```
p1: (set! x (* x x))
p2: (set! x (* x x x))
```

p1 gets x twice and sets it once.p2 gets x three times and sets it once.Similar to the analysis in [Exercise 3.38](./exercise_3_38.md).We will make

* p1 is split into basic operations p1-get-1, p1-get-2, and p1-set
* p2 is split into basic operations p2-get-1, p2-get-2, p2-get-3, and p2-set

Each operation can be interleaved.Generate the entire permutation of the list below

``` Scheme
(list 'p1-get-1 'p1-get-2 'p1-set 'p2-get-1 'p2-get-2 'p2-get-3 'p2-set)
```
The 7 terms are 7!= 5040 cases.But the get has to come before the set.There is the following mandatory order

* p1-get-1、p1-get-2、p1-set
* p2-get-1、p2-get-2、p2-get-3、p2-set

So you filter out some permutations, and all the possibilities are `7! / (3! * 4!) = 35` 。

We're going to simulate these 35 different sequences

output

```
(p1-get-1 p1-get-2 p1-set p2-get-1 p2-get-2 p2-get-3 p2-set): 1000000
(p1-get-1 p1-get-2 p2-get-1 p1-set p2-get-2 p2-get-3 p2-set): 100000
(p1-get-1 p1-get-2 p2-get-1 p2-get-2 p1-set p2-get-3 p2-set): 10000
.....
(p2-get-1 p2-get-2 p2-get-3 p1-get-1 p1-get-2 p2-set p1-set): 100
(p2-get-1 p2-get-2 p2-get-3 p1-get-1 p2-set p1-get-2 p1-set): 10000
(p2-get-1 p2-get-2 p2-get-3 p2-set p1-get-1 p1-get-2 p1-set): 1000000
'(100 1000 10000 100000 1000000)
```

So all the possible outcomes for x are `(100 1000 10000 100000 1000000)`。

### b)

When you serialize, p1 and p2 are inseparable.So we're left with two orders

```
x = 10
p1: (set! x (* x x))
p2: (set! x (* x x x))
```

And in that order, x is going to be 1 million.


### complete code

```Scheme
#lang racket

(require "ch3support.scm")

(define (element-of-set? x set)
  (cond ((null? set) false)
        ((equal? x (car set)) true)
        (else (element-of-set? x (cdr set)))))

(define (adjoin-set x set)
  (if (element-of-set? x set)
      set
      (cons x set)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define all-x-values '())

(define (run orders)
  (let ((x-value 10)
        (p1-x-1 0)
        (p1-x-2 0)
        (p2-x-1 0)
        (p2-x-2 0)
        (p2-x-3 0)
        )
    (define (process p)
      (cond ((eq? p 'p1-get-1) (set! p1-x-1 x-value))
            ((eq? p 'p1-get-2) (set! p1-x-2 x-value))
            ((eq? p 'p2-get-1) (set! p2-x-1 x-value))
            ((eq? p 'p2-get-2) (set! p2-x-2 x-value))
            ((eq? p 'p2-get-3) (set! p2-x-3 x-value))
            ((eq? p 'p1-set) (set! x-value (* p1-x-1 p1-x-2)))
            ((eq? p 'p2-set) (set! x-value (* p2-x-1 p2-x-2 p2-x-3)))
            ))
    (for-each process orders)
    
    (set! all-x-values (adjoin-set x-value all-x-values))
    (display orders)
    (display ": ")
    (display x-value)
    (newline)))

; Used to determine whether a is in front of b in the list.
; When a is in front of b, you must search for aand then search for b
(define (right-order? order a b)
  (and (memq a order)
       (memq b (memq a order))))

(define (gen-orders)
  (let* ((lst (list 'p1-get-1 'p1-get-2 'p1-set 'p2-get-1 'p2-get-2 'p2-get-3 'p2-set))
         (orders (permutations lst)))
    (filter (lambda (x)
              (and (right-order? x 'p1-get-1 'p1-get-2)
                   (right-order? x 'p1-get-2 'p1-set)
                   (right-order? x 'p2-get-1 'p2-get-2)
                   (right-order? x 'p2-get-2 'p2-get-3)
                   (right-order? x 'p2-get-3 'p2-set)))
            orders)))

(define orders (gen-orders))
(length orders)

(for-each run orders)
all-x-values
```
