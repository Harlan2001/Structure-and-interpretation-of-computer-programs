## exercise 3.52

### a)

The following code lists the value of sum after each expression is evaluated.And prints of  `stream-ref` and `display-stream`


``` Scheme
(define sum 0)
(define (accum x)
  (set! sum (+ x sum))
  sum)

(define seq (stream-map accum (stream-enumerate-interval 1 20)))
sum               ; 1

(define y (stream-filter even? seq))
sum               ; 6

(define z (stream-filter (lambda (x) (= (remainder x 5) 0))
                         seq))
sum               ; 10

(stream-ref y 7)  ; 136
sum               ; 136

(display-stream z)  ; 10 15 45 55 105 120 190 210
sum                 ; 210
```

Each time accum is called, the value of sum adds up.To know the value of sum, we need to analyze when to call accum.The delay here has a memorization process. After each accum evaluation, the result will be saved, and the next evaluation can be returned directly without changing the sum value.In other words, with the memorization process, the result of accum evaluation is the same each time. We can list the following table.

(Note: For the sake of description, it is written as the evaluation of accum without causing any misunderstanding.But in fact, delay's memorization process, is to accum packaging, returned a lambda, not the original accum function.)

```
(accum 1) = 1
(accum 2) = 3
(accum 3) = 6
(accum 4) = 10
(accum 5) = 15
(accum 6) = 21
(accum 7) = 28
(accum 8) = 36
(accum 9) = 45
(accum 10) = 55
(accum 11) = 66
(accum 12) = 78
(accum 13) = 91
(accum 14) = 105
(accum 15) = 120
(accum 16) = 136
(accum 17) = 153
(accum 18) = 171
(accum 19) = 190
(accum 20) = 210
```

When we define seq, we need to value the first element `(stream-car seq)`, so

```
(accum 1) = 1
```

When seq is defined, `sum = 1`。

When we define y, we look for the first even number from seq.seq has been evaluated `(accum 1)`. So we start at 2 and evaluate it

```
1
(accum 2) = 3
(accum 3) = 6	  ; first even number
```
Stop when we find it.So once we define y,`sum = 6`。

When you define z, just like you defined y, you look for the first number that fits.So we start at 2 and evaluate it

```
1
(accum 2) = 3
(accum 3) = 6
(accum 4) = 10 ; The first number that fits
```

Stop when you find it.So if we define z, `sum = 10`。Note that when we evaluate  `(accum 2)` again, we can return it without changing the value of sum

When evaluating `(stream-ref y 7)`you need to look for a number with an index of 7.Since `(stream-car y)`  has been evaluated to`(accum 3) = 6` is the first even number, starting at 4 and evaluating in order.

```
6                   ;  index = 0
(accum 4) = 10      ;  index = 1
(accum 5) = 15
(accum 6) = 21
(accum 7) = 28      ;  index = 2
(accum 8) = 36      ;  index = 3
(accum 9) = 45
(accum 10) = 55
(accum 11) = 66     ;  index = 4
(accum 12) = 78     ;  index = 5
(accum 13) = 91
(accum 14) = 105
(accum 15) = 120    ;  index = 6
(accum 16) = 136    ;  index = 7
```

Therefore, the evaluation `(stream-ref y 7)` results in 136. After that `sum = 136`.

`(display-stream z)` prints z.The first element of z, `(stream-car z) = 10`, has been evaluated to `(accum 4)`， so starting with  `(accum 5)`, prints the value that meets the condition.

```
10                  ; Meet the criteria, print
(accum 5) = 15      ; Meet the criteria, print
(accum 6) = 21
(accum 7) = 28
(accum 8) = 36
(accum 9) = 45      ; Meet the criteria, print
(accum 10) = 55     ; Meet the criteria, print
(accum 11) = 66
(accum 12) = 78
(accum 13) = 91
(accum 14) = 105    ; Meet the criteria, print
(accum 15) = 120    ; Meet the criteria, print
(accum 16) = 136
(accum 17) = 153
(accum 18) = 171
(accum 19) = 190    ; Meet the criteria, print
(accum 20) = 210    ; Meet the criteria, print
```

`(display-stream z)` prints out `10 15 45 55 105 120 190 210`, and finally `sum = 210`。


### b)

The above result is based on delay having memory.If there is no memorization, accum is re-evaluated each time it is called.So this is going to be

``` Scheme
(define sum 0)
(define (accum x)
  (set! sum (+ x sum))
  sum)

(define seq (stream-map accum (stream-enumerate-interval 1 20)))
sum               ; 1

(define y (stream-filter even? seq))
sum               ; 6

(define z (stream-filter (lambda (x) (= (remainder x 5) 0))
                         seq))
sum               ; 15

(stream-ref y 7)  ; 162
sum               ; 162

(display-stream z)  ; 15 180 230 305
sum                 ; 362
```

When we define seq, similar to the memorization process in a), we need to value the first element`(stream-car seq)`, so

```
(accum 1) = 1
```

When seq is defined, `sum = 1`。

he definition of y is similar to the memory process in a), looking for the first even number.seq has been evaluated `(accum 1)`. So we start at 2 and evaluate it

```
1
(accum 2) = 3
(accum 3) = 6	  ; first even number
```
Stop when you find it.So once I define y,`sum = 6`。

When defining z, we need to start from `(accum 2)`、`(accum 3)` ... Evaluate in turn, looking for the first number that meets the condition.But there is no memorization, so calling `(accum 2)` again changes sum and evaluates it in turn

```
1
(accum 2) = 8
(accum 3) = 11
(accum 4) = 15 ; The first number that fits
```

Find a value that meets your criteria and stop.So if I define z,`sum = 15`。

When evaluating `(stream-ref y 7)` , `(stream-car y) = 6` is already an even number with index 0, having evaluated to `(accum 3)`. Start with`(accum 4)` and look for even numbers with an index of 7.Evaluation in turn
```
6                   ;  index = 0
(accum 4) = 19
(accum 5) = 24      ;  index = 1
(accum 6) = 30      ;  index = 2
(accum 7) = 37
(accum 8) = 45
(accum 9) = 54      ;  index = 3
(accum 10) = 64     ;  index = 4
(accum 11) = 75
(accum 12) = 87
(accum 13) = 100    ;  index = 5
(accum 14) = 114    ;  index = 6
(accum 15) = 129
(accum 16) = 145
(accum 17) = 162    ;  index = 7
```
At this point, after evaluating `(stream-ref y 7)` , `sum = 162`。

`(display-stream z)` prints z.The first element of z,  `(stream-car z) = 15`, is evaluated to  `(accum 4)`. So from the beginning of `(accum 5)`, in turn to evaluate the value of print to meet the conditions

```
15                  ; Meet the criteria, print
(accum 5) = 167
(accum 6) = 173
(accum 7) = 180     ; Meet the criteria, print
(accum 8) = 188
(accum 9) = 197
(accum 10) = 207
(accum 11) = 218
(accum 12) = 230    ; vMeet the criteria, print
(accum 13) = 243
(accum 14) = 257
(accum 15) = 272
(accum 16) = 288
(accum 17) = 305    ; Meet the criteria, print
(accum 18) = 323
(accum 19) = 342
(accum 20) = 362
```

Then `(display-stream z)` prints `15 180 230 305`. `sum = 362`。


### Complete test code
```Scheme
#lang racket

(require "stream.scm")

(define (stream-map proc . argstreams)
  (if (stream-null? (car argstreams))
      the-empty-stream
      (cons-stream 
        (apply proc (map stream-car argstreams))
        (apply stream-map
               (cons proc (map stream-cdr argstreams))))))

;;;;;;;;;;;;;;;;;;;;;
;;delay the result of the memory process

(define sum 0)

(define (accum x)
  (set! sum (+ x sum))
  sum)

(define seq (stream-map accum (stream-enumerate-interval 1 20)))
sum               ; 1

(define y (stream-filter even? seq))
sum               ; 6

(define z (stream-filter (lambda (x) (= (remainder x 5) 0))
                         seq))
sum               ; 10

(stream-ref y 7)  ; 136
sum               ; 136

(display-stream z)  ; 10 15 45 55 105 120 190 210
sum                 ; 210
```
