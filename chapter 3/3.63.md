## P235 - [练习 3.63]

`sqrt-stream` represents the iteration as a stream. To compute the value of the NTH term, first compute the value of the n-1 term.

### a)

The original implementation is

``` Scheme
(define (sqrt-stream x)
  (define guesses
    (cons-stream 1.0
                 (stream-map (lambda (guess)
                               (sqrt-improve guess x))
                             guesses)))
  guesses)
```

`stream-map` passes the same stream guesses.If delay is optimized for memory, each item in guesses will be calculated only once.

Louis' version

``` Scheme
(define (sqrt-stream x)
  (cons-stream 1.0
               (stream-map (lambda (guess)
                             (sqrt-improve guess x))
                           (sqrt-stream x))))
```

Each time `stream-map`  returns a different `(sqrt-stream x)`, although the value of its entry is the same, but because it is a different stream, the results are not reusable.Under the implementation of Louis, when you compute the second term, you need to compute the first term.When the third term is computed, the second term is computed again, and the first term is computed again.Then the fourth term is computed, and the third, second, and first term are computed again.

Under the optimization of delay memory process, the original implementation efficiency is O(n).Louis' version, the efficiency becomes O(n^2).
### b)

When delay has no memory procedure optimization, the original implementation, even though it is the same stream guesses, is not reusable.In this case, each term has to be double computed, so the efficiency becomes O(n^2).

Louis's version itself did not enjoy the benefits of the memory process, even if delay did not have the memory process, it would not be affected.The efficiency is still O(n^2).

There is no difference in efficiency between the two versions.

### verification
                           
Add some print information in `sqrt-improve` .

``` Scheme
(define (sqrt-improve guess x)
  (display "sqrt-improve: guess= ")
  (display guess)
  (newline)
  (average guess (/ x guess)))
```                             

Under the delay memory process optimization.output

```
(sqrt-stream 2)
1.0
sqrt-improve: guess= 1.0
1.5
sqrt-improve: guess= 1.5
1.4166666666666665
sqrt-improve: guess= 1.4166666666666665
1.4142156862745097
sqrt-improve: guess= 1.4142156862745097
============
(sqrt-stream-2 2)
1.0
sqrt-improve: guess= 1.0
1.5
sqrt-improve: guess= 1.0
sqrt-improve: guess= 1.5
1.4166666666666665
sqrt-improve: guess= 1.0
sqrt-improve: guess= 1.5
sqrt-improve: guess= 1.4166666666666665
1.4142156862745097
sqrt-improve: guess= 1.0
sqrt-improve: guess= 1.5
sqrt-improve: guess= 1.4166666666666665
sqrt-improve: guess= 1.4142156862745097
============
```

See the original version (sqrt-stream), each computation more than one item, only one more call `sqrt-improve`. Louis version (sqrt-stream-2), each time will be repeated, the calculation of the further items, the more times to call `sqrt-improve` .

Modify [stream.scm](./stream.scm) to switch delay to non-memory optimization.This is the program output.

```
(sqrt-stream 2)
1.0
sqrt-improve: guess= 1.0
1.5
sqrt-improve: guess= 1.0
sqrt-improve: guess= 1.5
1.4166666666666665
sqrt-improve: guess= 1.0
sqrt-improve: guess= 1.5
sqrt-improve: guess= 1.4166666666666665
1.4142156862745097
sqrt-improve: guess= 1.0
sqrt-improve: guess= 1.5
sqrt-improve: guess= 1.4166666666666665
sqrt-improve: guess= 1.4142156862745097
============
(sqrt-stream-2 2)
1.0
sqrt-improve: guess= 1.0
1.5
sqrt-improve: guess= 1.0
sqrt-improve: guess= 1.5
1.4166666666666665
sqrt-improve: guess= 1.0
sqrt-improve: guess= 1.5
sqrt-improve: guess= 1.4166666666666665
1.4142156862745097
sqrt-improve: guess= 1.0
sqrt-improve: guess= 1.5
sqrt-improve: guess= 1.4166666666666665
sqrt-improve: guess= 1.4142156862745097
============
```

It is known that `sqrt-stream` and `sqrt-stream-2` double calculate each item, and the output information is no difference.



### Complete code
```Scheme
#lang racket

(require "stream.scm")
(require "infinite_stream.scm")

(define (average x y) (/ (+ x y) 2))
(define (square x) (* x x))

(define (sqrt-improve guess x)
  (display "sqrt-improve: guess= ")
  (display guess)
  (newline)
  (average guess (/ x guess)))

(define (sqrt-stream x)
  (define guesses
    (cons-stream 1.0
                 (stream-map (lambda (guess)
                               (sqrt-improve guess x))
                             guesses)))
  guesses)

(define (sqrt-stream-2 x)
  (cons-stream 1.0
               (stream-map (lambda (guess)
                             (sqrt-improve guess x))
                           (sqrt-stream-2 x))))

;;;;;;;;;;;;;;;;;;
(define (display-stream-withmsg msg s n)
  (display msg)
  (newline)
  (display-stream-n s n)
  (display "============")
  (newline))

(display-stream-withmsg "(sqrt-stream 2)" (sqrt-stream 2) 4)
(display-stream-withmsg "(sqrt-stream-2 2)" (sqrt-stream-2 2) 4)
```
