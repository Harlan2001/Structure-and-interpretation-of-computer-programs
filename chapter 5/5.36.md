## exercise 5.36

### a)

Our compiler evaluates operands from right to left.For example, compile the code `(f 1 2)` and the result is

```
((assign proc (op lookup-variable-value) (const f) (reg env))
 (assign val (const 2))
 (assign argl (op list) (reg val))
 (assign val (const 1))
 (assign argl (op cons) (reg val) (reg argl))
 (test (op primitive-procedure?) (reg proc))
 ...)
```

Obviously, we evaluate 2 and then we evaluate 1.

### b)

In [compiler code](./ch5-compiler.scm), it is the procedure `construct-arglist` that determines the order in which parameters are evaluated.If the order of evaluation is changed from left to right, change to

``` Scheme
(define (construct-arglist operand-codes)
  ;; (let ((operand-codes (reverse operand-codes))) So let's get rid of this line and not reverse it
  (if (null? operand-codes)
      (make-instruction-sequence '() '(argl)
       '((assign argl (const ()))))
      (let ((code-to-get-last-arg
             (append-instruction-sequences
              (car operand-codes)
              (make-instruction-sequence '(val) '(argl)
               '((assign argl (op list) (reg val)))))))
        (if (null? (cdr operand-codes))
            code-to-get-last-arg
            (preserving '(env)
             code-to-get-last-arg
             (code-to-get-rest-args
              (cdr operand-codes)))))))

(define (code-to-get-rest-args operand-codes)
  (let ((code-for-next-arg
         (preserving '(argl)
          (car operand-codes)
          (make-instruction-sequence '(val argl) '(argl)
           '((assign argl
              (op adjoin-arg) (reg val) (reg argl))))))) ;; We use (op adjoin-arg) instead of (op cons)
    (if (null? (cdr operand-codes))
        code-for-next-arg
        (preserving '(env)
         code-for-next-arg
         (code-to-get-rest-args (cdr operand-codes))))))
```

### c)

The implementation of `adjoin-arg` is

``` Scheme
(define (adjoin-arg arg arglist)
  (append arglist (list arg)))
``` 

`adjoin-arg` uses `append` which is slower than the original `cons` .In the original right-to-left implementation,  `reverse` is called to reverse the argument table.There seems to be little difference in efficiency.

Note, however, that `adjoin-arg` instead of `cons` is executed at runtime.The call to `reverse` the argument table only at compile time, and does not need to reverse the argument table at run time.Therefore, at run time, it is more efficient to evaluate parameters from right to left.

Another way to evaluate from left to right is to call `cons` in order (so that the argument list is in reverse order) and then finally call `reverse` .This implementation puts both `cons` and `reverse` into execution.It's still going to be slower than the original right-to-left order of evaluation.
