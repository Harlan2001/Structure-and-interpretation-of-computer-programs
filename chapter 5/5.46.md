## exercise 5.46

### fib compilation

Build the code to run fib and type it in turn

here is the code

```Scheme
#lang sicp

;; compile fib

(#%require "ch5-eceval-compiler.scm")

(compile-and-go
  '(define (fib n)
     (if (< n 2)
         n
         (+ (fib (- n 1)) (fib (- n 2)))))
  )
```

``` Scheme
(fib 2)   ; (total-pushes = 17 maximum-depth = 5)
(fib 3)   ; (total-pushes = 27 maximum-depth = 8)
(fib 4)   ; (total-pushes = 47 maximum-depth = 11)
(fib 5)   ; (total-pushes = 77 maximum-depth = 14)
(fib 6)   ; (total-pushes = 127 maximum-depth = 17)
(fib 7)   ; (total-pushes = 207 maximum-depth = 20)
(fib 8)   ; (total-pushes = 337 maximum-depth = 23) 
(fib 9)   ; (total-pushes = 547 maximum-depth = 26)
(fib 10)  ; (total-pushes = 887 maximum-depth = 26)
```

Gets information about `total-pushes` and `maximum-depth`. Make a table

|  n               | 2  | 3  | 4  | 5  | 6  | 7  | 8  | 9  | 10 |
|--------------    |----|----|----|----|----|----|----|----|----|
| maximum-depth    | 5  | 8  | 11 | 14 | 17 | 20 | 23 | 26 | 26 |
| total-pushes     | 17 | 27 | 47 | 77 | 127| 207| 337| 547| 887|
| Fib(n+1)         | 2  | 3  | 5  | 8  | 13 | 21 | 34 | 55 | -  |

Using an analysis similar to [Exercise 5.29](./5.29.md), get

```
maximum-depth = 5 + (n - 2) * 3 = 3 * n - 1
total-pushes = 10 * Fib(n+1) - 3
```

### Dedicated fib machine

Execute a handmade special fib machine and get information on `total-pushes` and `maximum-depth`. Make a table

here is the code

```Scheme
#lang sicp

;; special fib machine

(#%require "ch5-regsim.scm")

(define fib-machine
  (make-machine
    '(continue n val)
    (list (list '< <)
          (list '- -)
          (list '+ +)
          )
    '(
      (perform (op initialize-stack))
      (assign continue (label fib-done))
    fib-loop
      (test (op <) (reg n) (const 2))
      (branch (label immediate-answer))
      ;; set up to compute Fib(n - 1)
      (save continue)
      (assign continue (label afterfib-n-1))
      (save n)                           ; save old value of n
      (assign n (op -) (reg n) (const 1)); clobber n to n - 1
      (goto (label fib-loop))            ; perform recursive call
    afterfib-n-1                         ; upon return, val contains Fib(n - 1)
      (restore n)
      (restore continue)
      ;; set up to compute Fib(n - 2)
      (assign n (op -) (reg n) (const 2))
      (save continue)
      (assign continue (label afterfib-n-2))
      (save val)                         ; save Fib(n - 1)
      (goto (label fib-loop))
    afterfib-n-2                         ; upon return, val contains Fib(n - 2)
      (assign n (reg val))               ; n now contains Fib(n - 2)
      (restore val)                      ; val now contains Fib(n - 1)
      (restore continue)
      (assign val                        ;  Fib(n - 1) +  Fib(n - 2)
              (op +) (reg val) (reg n)) 
      (goto (reg continue))              ; return to caller, answer is in val
    immediate-answer
      (assign val (reg n))               ; base case:  Fib(n) = n
      (goto (reg continue))
    fib-done
      (perform (op print-stack-statistics))
      )))

(define (loop start end f)
  (cond ((<= start end) 
         (f start)
         (loop (+ start 1) end f))))

(loop 2 10 (lambda (n)
             (newline)
             (display "n: ")
             (display n)
             (set-register-contents! fib-machine 'n n)
             (start fib-machine)
             (get-register-contents fib-machine 'val)
             ))
```


|  n               | 2  | 3  | 4  | 5  | 6  | 7  | 8  | 9  | 10 |
|--------------    |----|----|----|----|----|----|----|----|----|
| maximum-depth    | 2  | 4  | 6  | 8  | 10 | 12 | 14 | 16 | 18 |
| total-pushes     | 4  | 8  | 16 | 28 | 48 | 80 | 132| 216| 352|
| Fib(n+1)         | 2  | 3  | 5  | 8  | 13 | 21 | 34 | 55 | -  |

Using an analysis similar to [exercise 5.29](./5.29.md), get

```
maximum-depth = 2 + (n - 2) * 2 = 2 * n - 2
total-pushes = 4 * Fib(n+1) - 4
```

### Interpret fib

From [Exercise 5.29](./5.29.md), we use the evaluator to explain running a fib with stack information of

```
maximum-depth = 13 + (n - 2) * 5 = 5 * n + 3
total-pushes = 56 * Fib(n+1) - 40
```

### contrast

Compare the utilization of stack operations.The hand-built fib machine is superior to the compiled version, and the compiled version is superior to the interpreted execution.
