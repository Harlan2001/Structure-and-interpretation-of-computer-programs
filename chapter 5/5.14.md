## exercise 5.14

Based on [factorial machine](./fact-machine.scm), loop set n to print stack statistics.The printed information is as follows:

```
n: 1
(total-pushes = 0 maximum-depth = 0)
n: 2
(total-pushes = 2 maximum-depth = 2)
n: 3
(total-pushes = 4 maximum-depth = 4)
n: 4
(total-pushes = 6 maximum-depth = 6)
n: 5
(total-pushes = 8 maximum-depth = 8)
n: 6
(total-pushes = 10 maximum-depth = 10)
n: 7
(total-pushes = 12 maximum-depth = 12)
n: 8
(total-pushes = 14 maximum-depth = 14)
n: 9
(total-pushes = 16 maximum-depth = 16)
n: 10
(total-pushes = 18 maximum-depth = 18)
```

Make a table

|  n            | 1  | 2  | 3  | 4  | 5  | 6  | 7  | 8  | 9  | 10 |
|-------------- |----|----|----|----|----|----|----|----|----|----|
| total-pushes  | 0  | 2  | 4  | 6  | 8  | 10 | 12 | 14 | 16 | 18 |
| maximum-depth | 0  | 2  | 4  | 6  | 8  | 10 | 12 | 14 | 16 | 18 |

And when we observe it, we get the relationship

* `total-pushes = 2 * (n - 1)`
* `maximum-depth = 2 * (n - 1)`

### Complete code
```Scheme
#lang sicp

(#%require "ch5-regsim.scm")

(define fact-machine
  (make-machine
    '(continue n val)
    (list (list '= =)
          (list '- -)
          (list '* *)
          )
    '(
      (perform (op initialize-stack))
      (assign continue (label fact-done))     ; set up final return address
      fact-loop
      (test (op =) (reg n) (const 1))
      (branch (label base-case))
      ;; Set up for the recursive call by saving n and continue.
      ;; Set up continue so that the computation will continue
      ;; at after-fact when the subroutine returns.
      (save continue)
      (save n)
      (assign n (op -) (reg n) (const 1))
      (assign continue (label after-fact))
      (goto (label fact-loop))
      after-fact
      (restore n)
      (restore continue)
      (assign val (op *) (reg n) (reg val))   ; val now contains n(n - 1)!
      (goto (reg continue))                   ; return to caller
      base-case
      (assign val (const 1))                  ; base case: 1! = 1
      (goto (reg continue))                   ; return to caller
      fact-done
      (perform (op print-stack-statistics))
      )))

(define (loop start end f)
  (cond ((<= start end) 
         (f start)
         (loop (+ start 1) end f))))

(loop 1 10 (lambda (n)
             (newline)
             (display "n: ")
             (display n)
             (set-register-contents! fact-machine 'n n)
             (start fact-machine)
             (get-register-contents fact-machine 'val)
             ))
```
