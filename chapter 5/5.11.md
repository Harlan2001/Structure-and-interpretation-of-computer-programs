## exercise 5.11

### a)

In [Fibonacci Machine](./fib-machine.scm), the code in the `afterfib-n-2` tag is as follows

``` Scheme
afterfib-n-2                         ; upon return, val contains Fib(n - 2)
  (assign n (reg val))               ; n now contains Fib(n - 2)
  (restore val)                      ; val now contains Fib(n - 1)
  (restore continue)
  (assign val                        ;  Fib(n - 1) +  Fib(n - 2)
          (op +) (reg val) (reg n)) 
```

In the code above, n holds Fib(n-2), val holds Fib(n-1), and add them together.You can replace the first two instructions with one instruction.Modify to

``` Scheme
afterfib-n-2                        ; upon return, val contains Fib(n - 2)
  (restore n)                       ; n now contains Fib(n - 1)
  (restore continue)
  (assign val                       ; Fib(n - 2) + Fib(n - 1)
          (op +) (reg val) (reg n)) 
```

In the code above, you put n in Fib(n-1), val in Fib(n-2), and you add them.The result is the same.

### b)

On the basis of [ch5-regsim.scm](./ch5-regsim.scm), modify the `make-save` and `make-restore` functions:

``` Scheme
(define (make-save inst machine stack pc)
  (let* ((reg-name (stack-inst-reg-name inst))
         (reg (get-register machine reg-name)))
    (lambda ()
      (push stack (cons reg-name (get-contents reg)))
      (advance-pc pc))))

(define (make-restore inst machine stack pc)
  (let* ((reg-name (stack-inst-reg-name inst))
         (reg (get-register machine reg-name)))
    (lambda ()
      (let ((top (pop stack)))
        (if (eq? reg-name (car top))
            (begin
              (set-contents! reg (cdr top))    
              (advance-pc pc))
            (error "The value is not from register:" reg-name))))))
```

### c)

On the basis of [ch5-regsim.scm](./ch5-regsim.scm), the main thing is to modify the `make-register`, `make-save`  and `make-restore` functions to put the stack into the register.

``` Scheme
(define (make-register name)
  (let ((contents '*unassigned*)
        (stack (make-stack)))
    (define (dispatch message)
      (cond ((eq? message 'stack) stack)
            ((eq? message 'get) contents)
            ((eq? message 'set)
             (lambda (value) (set! contents value)))
            (else
              (error "Unknown request -- REGISTER" message))))
    dispatch))
    
(define (make-save inst machine pc)
  (let ((reg (get-register machine 
                           (stack-inst-reg-name inst))))
    (lambda ()
      (push (reg 'stack) (get-contents reg))
      (advance-pc pc))))

(define (make-restore inst machine pc)
  (let ((reg (get-register machine
                           (stack-inst-reg-name inst))))
    (lambda ()
      (set-contents! reg (pop (reg 'stack)))    
      (advance-pc pc))))    
```

In addition, `make-new-machine` and `make-execution-procedure` also need to be adjusted to delete the stack in the original machine

### c) The full code is here
```Scheme
#lang sicp

(#%require "ch5support.scm")
(#%require "ch5-regsim.scm")

(#%provide make-machine set-register-contents! start get-register-contents)

(redefine (make-register name)
  (let ((contents '*unassigned*)
        (stack (make-stack)))
    (define (dispatch message)
      (cond ((eq? message 'stack) stack)
            ((eq? message 'get) contents)
            ((eq? message 'set)
             (lambda (value) (set! contents value)))
            (else
              (error "Unknown request -- REGISTER" message))))
    dispatch))

(redefine (make-new-machine)
  (let* ((pc (make-register 'pc))
         (flag (make-register 'flag))
         (the-instruction-sequence '())
         (register-table
           (list (list 'pc pc) (list 'flag flag))))
    (let ((the-ops
            (list (list 'initialize-stack
                        (lambda () 
                          (for-each (lambda (reg-pair)
                                      (let ((stack ((cdr reg-pair) 'stack)))
                                        (stack 'initialize))
                                      register-table))))
                  ;;**next for monitored stack (as in section 5.2.4)
                  ;;  -- comment out if not wanted
                  (list 'print-stack-statistics
                        (lambda () 
                          (for-each (lambda (reg-pair)
                                      (let ((stack ((cdr reg-pair) 'stack)))
                                        (stack 'print-statistics))
                                      register-table)))))))
      (define (allocate-register name)
        (if (assoc name register-table)
            (error "Multiply defined register: " name)
            (set! register-table
                  (cons (list name (make-register name))
                        register-table)))
        'register-allocated)
      (define (lookup-register name)
        (let ((val (assoc name register-table)))
          (if val
              (cadr val)
              (error "Unknown register:" name))))
      (define (execute)
        (let ((insts (get-contents pc)))
          (if (null? insts)
              'done
              (begin
                ((instruction-execution-proc (car insts)))
                (execute)))))
      (define (dispatch message)
        (cond ((eq? message 'start)
               (set-contents! pc the-instruction-sequence)
               (execute))
              ((eq? message 'install-instruction-sequence)
               (lambda (seq) (set! the-instruction-sequence seq)))
              ((eq? message 'allocate-register) allocate-register)
              ((eq? message 'get-register) lookup-register)
              ((eq? message 'install-operations)
               (lambda (ops) (set! the-ops (append the-ops ops))))
              ((eq? message 'operations) the-ops)
              (else (error "Unknown request -- MACHINE" message))))
      dispatch)))

(redefine (update-insts! insts labels machine)
  (let ((pc (get-register machine 'pc))
        (flag (get-register machine 'flag))
        (ops (machine 'operations)))
    (for-each
      (lambda (inst)
        (set-instruction-execution-proc! 
          inst
          (make-execution-procedure
            (instruction-text inst) labels machine
            pc flag ops)))
      insts)))

(define (make-execution-procedure inst labels machine
                                  pc flag ops)
  (cond ((eq? (car inst) 'assign)
         (make-assign inst machine labels ops pc))
        ((eq? (car inst) 'test)
         (make-test inst machine labels ops flag pc))
        ((eq? (car inst) 'branch)
         (make-branch inst machine labels flag pc))
        ((eq? (car inst) 'goto)
         (make-goto inst machine labels pc))
        ((eq? (car inst) 'save)
         (make-save inst machine pc))
        ((eq? (car inst) 'restore)
         (make-restore inst machine pc))
        ((eq? (car inst) 'perform)
         (make-perform inst machine labels ops pc))
        (else (error "Unknown instruction type -- ASSEMBLE"
                     inst))))

(define (make-save inst machine pc)
  (let ((reg (get-register machine 
                           (stack-inst-reg-name inst))))
    (lambda ()
      (push (reg 'stack) (get-contents reg))
      (advance-pc pc))))

(define (make-restore inst machine pc)
  (let ((reg (get-register machine
                           (stack-inst-reg-name inst))))
    (lambda ()
      (set-contents! reg (pop (reg 'stack)))    
      (advance-pc pc))))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define test-machine
  (make-machine
    '(x y)
    (list)
    '(
      (save y)
      (save x)
      (restore y)
      (restore x)
      )))

(set-register-contents! test-machine 'x 100)
(set-register-contents! test-machine 'y 200)
(start test-machine)
(get-register-contents test-machine 'x)
(get-register-contents test-machine 'y)
```

### Test

The following code

``` Scheme
#lang sicp

(#%require "ch5-regsim.scm")
;; (#%require "exercise_5_11_c.scm")

(define test-machine
  (make-machine
    '(x y)
    (list)
    '(
      (save y)
      (save x)
      (restore y)
      (restore x)
      )))

(set-register-contents! test-machine 'x 100)
(set-register-contents! test-machine 'y 200)
(start test-machine)
(get-register-contents test-machine 'x)
(get-register-contents test-machine 'y)
```

The stack implemented as a), finally x = 200, y = 100.

A stack implemented in accordance with b) emits an error message, `The value is not from register: y`ã€‚

The stack is implemented as c), and finally x = 100 and y = 200.
