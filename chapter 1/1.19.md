## exercise 1.19

This problem is actually a variation of Fibonacci's matrix form.Fibonacci's matrix form is:

$$\begin{bmatrix} 1 & 1 \\
1 & 0 \end{bmatrix}^{n}= \begin{bmatrix}F_{n+1} & F_{n} \\
F_{n} &  F_{n-1}\end{bmatrix}= \begin{bmatrix}F_{n-1} + F_{n} & F_{n} \\
F_{n} &  F_{n-1}\end{bmatrix}$$

------

The action rule of the variation $T_{pq}$ on the dual (a, b) is

```
a ← bq + aq + ap
b ← bp + aq
```

We could actually write

$$\begin{bmatrix}a' \\
b' \end{bmatrix} = \begin{bmatrix}p + q & q \\
q & p \end{bmatrix}\begin{bmatrix}a \\
b \end{bmatrix}=\begin{bmatrix}bq + aq + ap \\
bp + aq \end{bmatrix}$$

Here $T_{pq}$，the matrix form is

$$T_{pq}=\begin{bmatrix}p + q & q \\
q & p \end{bmatrix}$$

so

$$T_{pq}^{2}=\begin{bmatrix}p + q & q \\
q & p \end{bmatrix}^{2} = \begin{bmatrix}(p+q)^{2}+q^{2} & 2pq + q^{2} \\
2pq + q^{2} & p^{2} + q^{2} \end{bmatrix}=\begin{bmatrix}(p^{2} + q^{2})+(2pq + q^{2}) & 2pq + q^{2} \\
2pq + q^{2} & p^{2} + q^{2} \end{bmatrix}$$

Think of the rightmost part as the new $T_{p'q'}$ transformation.There are

$$T_{p'q'}=\begin{bmatrix}p' + q' & q' \\
q' & p' \end{bmatrix}=T_{pq}^{2}=\begin{bmatrix}(p^{2} + q^{2})+(2pq + q^{2}) & 2pq + q^{2} \\
2pq + q^{2} & p^{2} + q^{2} \end{bmatrix}$$

After comparing, we get the result

```
p ← pp + qq
q ← 2pq + qq
```

------

The final code is:

``` Scheme
#lang racket

(define (fast-fib n)
  (fib-iter 1 0 0 1 n))

(define (fib-iter a b p q count)
  (cond ((= count 0) b)
        ((even? count)
         (fib-iter a
                   b
                   (+ (* p p) (* q q))    ;compute p'
                   (+ (* 2 p q) (* q q))  ;compute q'
                   (/ count 2)))
        (else (fib-iter (+ (* b q) (* a q) (* a p))
                        (+ (* b p) (* a q))
                        p
                        q
                        (- count 1)))))

;;;;;;;;;;;;;;;;;;;
(module* test #f
  (require rackunit)
  (define (for-loop n last op)
    (cond ((<= n last)
           (op n)
           (for-loop (+ n 1) last op))))
  
  (define (fib n)
    (cond ((= n 0) 0)
          ((= n 1) 1)
          (else (+ (fib (- n 1)) 
                   (fib (- n 2))))))
  
  (define (check i)
    (check-equal? (fib i) (fast-fib i)))
  
  (for-loop 0 20 check)
)
```
