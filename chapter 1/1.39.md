## exercise 1.39

The continued fraction of tangent has a minus sign in it

$$tan(x)=\frac{x}{1-\frac{x^{2}}{3-\frac{x^{2}}{5-...}}}$$

While practicing 1.37 with a plus sign seems a little different.

$$f(x)=\frac{N_{1}}{D_{1}+\frac{N_{2}}{D_{2}+\frac{N_{3}}{D_{3}+...}}}$$

The easiest way to do this is to duplicate `cont_frac`from Exercise 1.37, changing the plus sign to a minus sign.In real projects, however, copying and pasting code is not recommended.

I can rewrite the continued fraction of tangent of x as this, then `cont_frac`can be reused.

$$tan(x)=\frac{x}{1+\frac{-x^{2}}{3+\frac{-x^{2}}{5+...}}}$$

-----

### Code

``` Scheme
#lang racket

(define (cont-frac n-fn d-fn k)
  (define (impl i)
    (if (= i k)
        (/ (n-fn i) (d-fn i))
        (/ (n-fn i) (+ (d-fn i) (impl (+ i 1))))))
  (impl 1))

(define (tran-cf x k)
  (define (n-fn i)
    (if (= i 1)
        x
        (- (* x x))))
  (define (d-fn i)
    (- (* 2 i) 1))
  (cont-frac n-fn d-fn k))

;;;;;;;;;;;;;
(module* test #f
  (require rackunit)
  (define (for-loop n last op)
    (cond ((<= n last)
           (op n)
           (for-loop (+ n 1) last op))))
  
  (define (test-n n)
    (let ((x (/ n (* 2 3.1415926))))
      (check-= (tran-cf x 100) (tan x) 0.0001)))
  
  (for-loop 0 360 test-n)
)

```
