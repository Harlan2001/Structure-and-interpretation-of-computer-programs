## exercise 1.37

Write the truncation formula of continued fractions as

$$f(i, k) = \frac{N_{i}}{D_{i}+\frac{N_{i+1}}{...+\frac{N_{k}}{D_{k}}}}$$

So we have

$$\begin{aligned}&f(1, k)=\frac{N_{1}}{D_{1}+\frac{N_{2}}{...+\frac{N_{k}}{D_{k}}}}=\frac{N_{1}}{D_{1}+f(2, k)}\\&f(2, k)=\frac{N_{2}}{D_{2}+\frac{N_{3}}{...+\frac{N_{k}}{D_{k}}}}=\frac{N_{2}}{D_{2}+f(3, k)} \\&... \\&f(k, k)=\frac{N_{k}}{D_{k}}\end{aligned}$$

That is, there is a recursive formula:

$$\begin{aligned}&f(i, k)=\frac{N_{i}}{D_{i}+f(i + 1, k)}\\&f(k, k)=\frac{N_{k}}{D_{k}}\end{aligned}$$


To get an iterative version, reverse the order.Sequential computation sequence

```
f(k, k) -> f(k - 1, k) -> f(k - 2, k) ... f(1, k)
```

### code

``` Scheme
#lang racket

; Recursive version
(define (cont-frac n-fn d-fn k)
  (define (impl i)
    (if (= i k)
        (/ (n-fn i) (d-fn i))
        (/ (n-fn i) (+ (d-fn i) (impl (+ i 1))))))
  (impl 1))

; Iterative version
(define (cont-frac-2 n-fn d-fn k)
  (define (iter i ret)
    (if (< i 1)
        ret
        (iter (- i 1) 
              (/ (n-fn i) (+ (d-fn i) ret)))))
  (iter (- k 1) (/ (n-fn k) (d-fn k))))

(define (golden-ratio n)
  (define (fn x) 1)
  (cont-frac fn fn n))

(define (golden-ratio-2 n)
  (define (fn x) 1)
  (cont-frac-2 fn fn n))

;;;;;;;;;;;;;;;;;;;;;
(exact->inexact (golden-ratio 10))
(exact->inexact (golden-ratio 11))

(require rackunit)
(check-= (golden-ratio 10) (golden-ratio-2 10) 0.0001)
(check-= (golden-ratio 11) (golden-ratio-2 11) 0.0001)
```

See exercise 1.13

The precise value of $\frac{1}{\phi}=\frac{2}{1+\sqrt{5}}=\phi-1$ is

```
0.61803398874989
```

and

```
(golden-ratio 10) = 0.6179775280898876
(golden-ratio 11) = 0.6180555555555556
```

As long as k = 11, the first four decimal places of the result are exactly the same.
