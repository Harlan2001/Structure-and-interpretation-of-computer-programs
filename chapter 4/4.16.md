## exercise 4.16

Merge the code from [exercise 4.6](./4.6.md) into [mceval.scm](./mceval.scm) and modify the tests further.

### a)

Add a judgment to `lookup-variable-value` and change it as follows:

``` Scheme
(define (lookup-variable-value var env)
  (define (env-loop env)
    (define (scan vars vals)
      (cond ((null? vars)
             (env-loop (enclosing-environment env)))
            ((eq? var (car vars))
             (if (eq? (car vals) '*unassigned*)       
                 (error "variable is unassigned" var)
                 (car vals)))
            (else (scan (cdr vars) (cdr vals)))))
    (if (eq? env the-empty-environment)
        (error "Unbound variable" var)
        (let ((frame (first-frame env)))
          (scan (frame-variables frame)
                (frame-values frame)))))
  (env-loop env))
```

### b)

`scan-out-defines` implements as follow, using P78's P78 çš„ `filter` function.

``` Scheme
(define (filter predicate sequence)
  (if (null? sequence)
      '()
      (if (predicate (car sequence))
          (cons (car sequence) (filter predicate (cdr sequence)))
          (filter predicate (cdr sequence)))))

(define (scan-out-defines body)
  (define (body-defines body)
    (filter definition? body))
  (define (name-unassigned defines)
    (map (lambda (exp)
           (list (definition-variable exp) ''*unassigned*))
         defines))
  (define (defines->let-body body)
    (map (lambda (exp)
           (if (definition? exp)
               (list 'set! (definition-variable exp) (definition-value exp))
               exp))
         body))
  (let ((defines (body-defines body)))
    (if (null? defines)
        body
        (list (append (list 'let (name-unassigned defines))
                      (defines->let-body body))))))
```

In the above implementation, the order of define and other expressions is preserved.For example

``` Scheme
(
 (define a 1)
 (+ a b)
 (define b 1)
)
```

Convert to

``` Scheme
(
 (let ((a '*unassigned*)
       (b '*unassigned*)) 
   (set! a 1)  ;; Preserving order
   (+ a b) 
   (set! b 1))
)
```

Instead of converting to
 
``` Scheme
(
 (let ((a '*unassigned*)
       (b '*unassigned*)) 
   (set! a 1)  ;; Put all the converted set!in front (not good)
   (set! b 1) 
   (+ a b))
)
``` 

After preserving the order, executing the following code returns an error message, which is more reasonable.

``` Scheme
(define (f)
  (define a 1)
  (+ a b)  ;; variable is unassigned b
  (define b 1))
(f)
```

### c)

Install `scan-out-defines` in `make-procedure`, so that the conversion is defined only once.If installed in `procedure-body` , you repeatedly call `scan-out-defines` each time you get the body.

The implementation of  `make-procedure` is modified to:
 
``` Scheme
(define (make-procedure parameters body env)
  (list 'procedure parameters (scan-out-defines body) env))
```  
