## exercise 4.49

Change `parse-word` and `parse` to

``` Scheme
(define (amb-list lst) 
  (if (null? lst) 
      (amb) 
      (amb (car lst) (amb-list (cdr lst))))) 

(define (append list1 list2)
  (if (null? list1)
      list2
      (cons (car list1) (append (cdr list1) list2))))

(define (parse-word word-list)
  (require (not (null? *unparsed*)))
  (set! *unparsed* (cdr *unparsed*))
  (let ((word (amb-list (cdr word-list))))
    (set! *sentence* (append *sentence* (list word)))
    (list (car word-list) word)))

(define *unparsed* '())
(define *sentence* '())

(define (parse input)
  (set! *sentence* '())
  (set! *unparsed* input)
  (let ((sent (parse-sentence)))
    (require (null? *unparsed*))
    *sentence*))
```

Add `*sentence*` to hold the generated sentence.

Call `(parse '(1 2 3))` means that the generated sentence contains three words. The first 15 generated sentences are:

``` Scheme
;; (parse '(1 2 3))
(the student studies)
(the student lectures)
(the student eats)
(the student sleeps)
(the professor studies)
(the professor lectures)
(the professor eats)
(the professor sleeps)
(the cat studies)
(the cat lectures)
(the cat eats)
(the cat sleeps)
(the class studies)
(the class lectures)
(the class eats)
...
```

Call `(parse '(1 2 3 4 5 6 7 8 9))`, indicating that the generated sentence contains 9 words, and the first 15 generated sentences are:

``` Scheme
;; (parse '(1 2 3 4 5 6 7 8 9))
(the student studies for the student for the student)
(the student studies for the student for the professor)
(the student studies for the student for the cat)
(the student studies for the student for the class)
(the student studies for the student for a student)
(the student studies for the student for a professor)
(the student studies for the student for a cat)
(the student studies for the student for a class)
(the student studies for the student to the student)
(the student studies for the student to the professor)
(the student studies for the student to the cat)
(the student studies for the student to the class)
(the student studies for the student to a student)
(the student studies for the student to a professor)
(the student studies for the student to a cat)
...
```

###Complete code
```Scheme
#lang sicp

(#%require "ambeval.scm")

(easy-ambeval 10 '(begin

(define (require p)
  (if (not p) (amb)))

(define nouns '(noun student professor cat class))
(define verbs '(verb studies lectures eats sleeps))
(define articles '(article the a))
(define prepositions '(prep for to in by with))

(define (parse-sentence)
  (list 'sentence
        (parse-noun-phrase)
        (parse-verb-phrase)))

(define (parse-noun-phrase)
  (define (maybe-extend noun-phrase)
    (amb noun-phrase
         (maybe-extend (list 'noun-phrase
                             noun-phrase
                             (parse-prepositional-phrase)))))
  (maybe-extend (parse-simple-noun-phrase)))

(define (parse-prepositional-phrase)
  (list 'prep-phrase
        (parse-word prepositions)
        (parse-noun-phrase)))

(define (parse-verb-phrase)
  (define (maybe-extend verb-phrase)
    (amb verb-phrase
         (maybe-extend (list 'verb-phrase
                             verb-phrase
                             (parse-prepositional-phrase)))))
  (maybe-extend (parse-word verbs)))

(define (parse-simple-noun-phrase)
  (list 'simple-noun-phrase
        (parse-word articles)
        (parse-word nouns)))

(define (amb-list lst) 
  (if (null? lst) 
      (amb) 
      (amb (car lst) (amb-list (cdr lst))))) 

(define (append list1 list2)
  (if (null? list1)
      list2
      (cons (car list1) (append (cdr list1) list2))))

(define (parse-word word-list)
  (require (not (null? *unparsed*)))
  (set! *unparsed* (cdr *unparsed*))
  (let ((word (amb-list (cdr word-list))))
    (set! *sentence* (append *sentence* (list word)))
    (list (car word-list) word)))

(define *unparsed* '())
(define *sentence* '())

(define (parse input)
  (set! *sentence* '())
  (set! *unparsed* input)
  (let ((sent (parse-sentence)))
    (require (null? *unparsed*))
    *sentence*))

))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(define (print-list lst)
  (for-each (lambda (x) (display x) (newline)) lst))

(print-list (easy-ambeval 15 '(begin
                                (parse '(1 2 3))
                                )))

(print-list (easy-ambeval 15 '(begin
                                (parse '(1 2 3 4 5 6 7 8 9))
                                )))

```
