## exercise 4.36

[exercise 4.35](./4.35.scm) 的程序中，If  `an-integer-starting-from` is used instead of `integer-between`, i, j and k will have infinite choices.Especially for k, you need to search for endless options, i, j will not be updated.And condition

``` Scheme
(require (= (+ (* i i) (* j j)) (* k k)))
```
And if the conditions aren't met, it's a constant loop.

The right way is to put k first.Use `an-integer-between` to search for i and j, so that both i and j have a stop condition.The code is as follows:

``` Scheme
(define (a-pythagorean-triple)
  (let ((k (an-integer-starting-from 1)))
    (let ((j (an-integer-between 1 k)))
      (let ((i (an-integer-between 1 j)))
        (require (= (+ (* i i) (* j j)) (* k k)))
        (list i j k)))))
```

### Complete code
```Scheme
#lang sicp

(#%require "ambeval.scm")

(easy-ambeval 10 '(begin
                    
(define (require p)
  (if (not p) (amb)))

(define (an-integer-between low high)
  (require (<= low high))
  (amb low (an-integer-between (+ low 1) high)))

(define (an-integer-starting-from n)
  (amb n (an-integer-starting-from (+ n 1))))           

(define (a-pythagorean-triple)
  (let ((k (an-integer-starting-from 1)))
    (let ((j (an-integer-between 1 k)))
      (let ((i (an-integer-between 1 j)))
        (require (= (+ (* i i) (* j j)) (* k k)))
        (list i j k)))))

(a-pythagorean-triple)

))
```
