## exercise 4.21

### a)

The `lambda` nesting in the original code looks complicated.Let's just say define, just to simplify things.
 
``` Scheme
(define (func n)
  (define (f-gen fact)
    (fact fact n))
  (define (fact ft k)
    (if (= k 1)
        1
        (* k (ft ft (- k 1)))))
  (f-gen fact))
(func 10)
```

Note that the code above, while giving each lambda a name, is completely non-recursive.The above code can be further simplified, which is equivalent to

``` Scheme
(define (fact self k)
  (if (= k 1)
      1
      (* k (self self (- k 1)))))
(fact fact 10)
```

It's a little trick.Recursive functions need to call themselves, and since recursion can't be used, we can add one more argument (that is, the argument to self) and pass ourselves along when called.

-----

Now let's write some code like this to compute Fibonacci number 10.The first is the recursive function

``` Scheme
(define (fib n)
  (cond ((= n 0) 0)
        ((= n 1) 1)
        (else (+ (fib (- n 1)) 
                 (fib (- n 2))))))
(fib 10)
```

Add the self argument to make fib non-recursive.for

``` Scheme
(define (fib self n)
  (cond ((= n 0) 0)
        ((= n 1) 1)
        (else (+ (self self (- n 1)) 
                 (self self (- n 2))))))
(fib fib 10)
```

Finally, remove the function name and assemble it with lambda

``` Scheme
((lambda (n)
   ((lambda (fib)
      (fib fib n))
    (lambda (self n)
      (cond ((= n 0) 0)
            ((= n 1) 1)
            (else (+ (self self (- n 1)) 
                     (self self (- n 2))))))))
 10)
``` 

### b)

Just like a did, the original recursive function, we add a self argument. The event? and odd? here are reciprocal recursionï¼Œso it adds ev? and od? parameters the original function. Supplement the complete code as

``` Scheme
(define (f x)
  ((lambda (even? odd?)
     (even? even? odd? x))
   (lambda (ev? od? n)
     (if (= n 0) true (od? ev? od? (- n 1))))
   (lambda (ev? od? n)
     (if (= n 0) false (ev? ev? od? (- n 1))))))
```
