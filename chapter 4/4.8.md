## exercise 4.8

Modify the tests based on  [exercise 4.6](exercise_4_6.md) at  [mceval.scm](./mceval.scm) .

``` Scheme
(define (let? exp) (tagged-list? exp 'let))

(define (normal-let->combination exp)
  (define (let-body exp) (cddr exp))
  (define (let-vars exp) (map car (cadr exp)))
  (define (let-exps exp) (map cadr (cadr exp)))
  (cons (make-lambda (let-vars exp) 
                     (let-body exp)) 
        (let-exps exp)))

(define (named-let->combination exp)
  (define (let-name exp) (cadr exp))
  (define (let-body exp) (cdddr exp))
  (define (let-vars exp) (map car (caddr exp)))
  (define (let-exps exp) (map cadr (caddr exp)))
  (define (make-let-define exp) 
    (list 'define 
          (cons (let-name exp) (let-vars exp))
          (car (let-body exp))))
  (define (make-let-define-call exp)
    (list (make-let-define exp)
          (cons (let-name exp) (let-exps exp))))
  (cons (make-lambda '()
                     (make-let-define-call exp))
        '()))
                     
(define (let->combination exp)
  (define (named-let? exp) (symbol? (cadr exp)))
  (if (named-let? exp)
      (named-let->combination exp)
      (normal-let->combination exp)))
```      

Test code:

``` Scheme
(define (f a) a)

; ordinary let
(let ((a (f 'Hello))
      (b (f 'World)))
  (display a)
  (display b))

; Name let
(define (fib n)
  (let fib-iter ((a 1)
                 (b 0)
                 (count n))
    (if (= count 0)
        b
        (fib-iter (+ a b) a (- count 1)))))
(fib 10)          
```

Where `fib` is going to be

``` Scheme
(define (fib n)
  ((lambda () 
     (define (fib-iter a b count) 
       (if (= count 0)
           b 
           (fib-iter (+ a b) a (- count 1)))) 
     (fib-iter 1 0 n))))
```

Notice here that the `fib-iter` definition and `(fib-iter 1 0 n)` wrapped in a 'lambda', prevent `fib-iter` from having the same name.

