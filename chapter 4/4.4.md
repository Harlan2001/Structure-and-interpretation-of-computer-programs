## exercise 4.4

Modified on the basis of [mceval.scm](./mceval.scm) , eval add  `and?` 和 `or?` judgement。

``` Scheme
(define (eval exp env)
  (cond ((self-evaluating? exp) exp)
        ((variable? exp) (lookup-variable-value exp env))
        ((quoted? exp) (text-of-quotation exp))
        ((assignment? exp) (eval-assignment exp env))
        ((definition? exp) (eval-definition exp env))
        ((if? exp) (eval-if exp env))
        ((and? exp) (eval-and exp env))
        ((or? exp) (eval-or exp env))
        ((lambda? exp)
         (make-procedure (lambda-parameters exp)
                         (lambda-body exp)
                         env))
        ((begin? exp) 
         (eval-sequence (begin-actions exp) env))
        ((cond? exp) (eval (cond->if exp) env))
        ((application? exp)
         (apply (eval (operator exp) env)
                (list-of-values (operands exp) env)))
        (else
          (error "Unknown expression type -- EVAL" exp))))
```

### a)

The implementation of  `eval-if` and `eval-and` is

``` Scheme
(define (and? exp) (tagged-list? exp 'and))

(define (eval-and exp env)
  (define (loop exps env)
    (let ((first (eval (first-exp exps) env)))
      (cond ((last-exp? exps) first)
            (first (loop (rest-exps exps) env))
            (else #f))))
  (if (null? (cdr exp))
      #t
      (loop (cdr exp) env)))

(define (or? exp) (tagged-list? exp 'or))

(define (eval-or exp env)
  (define (loop exps env)
    (let ((first (eval (first-exp exps) env)))
      (cond ((last-exp? exps) first)
            (first first)
            (else (loop (rest-exps exps) env)))))
  (if (null? (cdr exp))
      #f
      (loop (cdr exp) env)))
```

Notice that the implementation of or does not follow the meaning of the problem.

According to the meaning，`(or 'a 'b)` should return #t.But here, as is the custom of DrRacket, `(or 'a 'b)`  returns 'a.If you must follow the problem, change the return value is fine.

### b)

and and or can also be implemented as derived expressions of if.For example

``` Scheme
(and a b c)
(or a b c)
```

Can be written as

``` Scheme
(if a
    (if b
        c
        #f)
    #f)
    
(if a
    #t
    (if b
        #t
        c)
```

So the idea is that the code can be written as

``` Scheme
(define (and? exp) (tagged-list? exp 'and))

(define (eval-and exp env)
  (define (expand-and-clauses exps)
    (if (last-exp? exps)
        (first-exp exps)
        (make-if (first-exp exps)
                 (expand-and-clauses (rest-exps exps))
                 #f)))
  (if (null? (cdr exp))
      #t
      (eval (expand-and-clauses (cdr exp)) env)))

(define (or? exp) (tagged-list? exp 'or))

(define (eval-or exp env)
  (define (expand-or-clauses exps)
    (if (last-exp? exps)
        (first-exp exps)
        (make-if (first-exp exps)
                 #t
                 (expand-or-clauses (rest-exps exps)))))
  (if (null? (cdr exp))
      #f
      (eval (expand-or-clauses (cdr exp)) env)))
```     

### Comparative discussion

In contrast to the implementation of or in a and b, they are not exactly equivalent.For example

* In a) ，`(or 'a 'b)` It returns 'a.
* In b) ，`(or 'a 'b)` It returns #t.

This is the implementation of b). if it is evaluated by an if statement, '(or 'a 'b)' will be converted to

``` Scheme
(if 'a
    #t
    'b)
```

Instead of converting to

``` Scheme
(if 'a
    'a
    'b)
```

This is because the statement  `(or (f a) (f b))` , if converted to

``` Scheme
(if (f a)
    (f a)
    (f b))
```

It is possible for the expression `(f a)` to be evaluated twice, which does not conform to the semantics of or.In addition, b) in fact, it is not entirely according to the passage.

* According to the meaning of question,  `(or #f 'a)` should return #t.
* he realization of b) , `(or #f 'a)` will return 'a。

I want the expression evaluation to be as close to DrRacket as possible.It's just going to be converted to an if statement, and it's going to be evaluated once, so it's not going to be exactly the same。
