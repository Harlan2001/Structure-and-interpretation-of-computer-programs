## exercise 4.13

We define `unbound` as the inverse of `define`  to eliminate the influence of  `define`. `define`only operates on the first frame, we define `unbound` as:

* Remove only the constraints of the first frame.
* If the corresponding var cannot be found in the first frame, an error message is returned.

Modify the test based on  [mceval.scm](./mceval.scm). Add a judgment to `eval` :

``` Scheme
((unbound? exp) (eval-unbound exp env))
```

The implementation code is:

``` Scheme
(define (unbound? exp)
  (tagged-list? exp 'unbound))

(define (eval-unbound exp env)
  (make-unbound! (cadr exp) env)
  'ok)

(define (make-unbound! var env)
  (let ((frame (first-frame env)))
    (define (scan pre-vars pre-vals vars vals)
      (cond ((null? vars)
             (error "Unbound variable -- MAKE-UNBOUND!" var))
            ((eq? var (car vars))
             (if (null? pre-vars)
                 (begin
                   (set-car! frame (cdr vars))
                   (set-cdr! frame (cdr vals)))
                 (begin
                   (set-cdr! pre-vars (cdr vars))
                   (set-cdr! pre-vals (cdr vals)))))
            (else (scan vars vals (cdr vars) (cdr vals)))))
    (scan '()
          '()
          (frame-variables frame)
          (frame-values frame))))                 
```

In test code

``` Scheme
(define a 1)
(unbound a)
(unbound a) ; Unbound variable -- MAKE-UNBOUND! a
```
The first one is unbound, removing the constraint on a.Then the second unbound cannot find the corresponding constraint, and an error message is returned.

