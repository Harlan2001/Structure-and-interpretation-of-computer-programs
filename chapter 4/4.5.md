## exercise 4.5

Modified based on  [mceval.scm](./mceval.scm) .

### a)

One way to do this is to keep cond as a derivative of if.`expand-clauses`  is changed toï¼š

``` Scheme
(define (cond-not-else-actions clauses)
  (if (eq? '=> (cadr clauses))
      (list (list (caddr clauses) (cond-predicate clauses))) ; Notice that there are two lists
      (cdr clauses)))

(define (expand-clauses clauses)
  (if (null? clauses)
      'false                          ; no else clause
      (let ((first (car clauses))
            (rest (cdr clauses)))
        (if (cond-else-clause? first)
            (if (null? rest)
                (sequence->exp (cond-actions first))
                (error "ELSE clause isn't last -- COND->IF"
                       clauses))
            (make-if (cond-predicate first)
                     (sequence->exp (cond-not-else-actions first)) 
                     (expand-clauses rest))))))
```

We can simply type in the following code to test.

``` Scheme
(cond ('a 'b))
(cond ('a => display))
```

There is a problem with this implementation, however, as shown in the following code

``` Scheme
(define (f a)
  (display a)
  (newline)
  a)
(cond ((f 'Hello) => display))
```
Will be converted to

``` Scheme
(if (f 'Hello)
    (display (f 'Hello))
    false)
```

So `(f 'Hello)` is evaluated twice.

### b)

To avoid repeated evaluation, you can implement `eval-cond`directly instead of treating it as a derivative of if.

``` Scheme
(define (eval-cond-clauses clauses env)
  (if (null? clauses)
      false
      (let ((first (car clauses))
            (rest (cdr clauses)))
        (if (cond-else-clause? first)
            (if (null? rest)
                (eval (sequence->exp (cond-actions first)) env)
                (error "ELSE clause isn't last -- EVAL-COND" clauses))
            (let ((predicate-val (eval (cond-predicate first) env)))
              (if (true? predicate-val)
                  (if (eq? '=> (cadr first))
                      (apply (eval (caddr first) env) (list predicate-val))
                      (eval-sequence (cond-actions first) env))
                  (eval-cond-clauses rest env)))))))

(define (eval-cond exp env)
  (eval-cond-clauses (cond-clauses exp) env))
```

The implementation of`eval-cond-clauses` is very similar to `expand-clauses` .
