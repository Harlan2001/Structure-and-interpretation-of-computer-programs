## exercise 4.9

### The syntactic definition of do

We use Scheme's R5RS standard. The syntax of do is defined as:

``` Scheme
(do ((<var1> <init1> <step1>)
     (<var2> <init2> <step2>)
     ...)
  (<test1> <exp1> <test2> <exp2> ...)
  <command> ...)
``` 

To a derived expression is equivalent to

``` Scheme
((lambda ()
   (define (do-iter <var1> <var2> ... <varN>)
     (cond (<test1> <exp1>)
           (<test2> <exp2>)
           ...
           (else
             <command> ...
             (do-iter <step1> <step2> ... <stepN>))))
   (do-iter <init1> <init2> ... <initN>)))
```

For example, the following code:

``` Scheme
(do ((i 0 (+ i 1)))
  ((> i 5) 'done)
  (display i)
  (newline))

(let ((x '(1 3 5 7 9)))
  (do ((x x (cdr x))
       (sum 0 (+ sum (car x))))
    ((null? x) sum))) 
```

Be equivalent to

``` Scheme
((lambda ()
   (define (do-iter i)
     (cond ((> i 5) 'done)
           (else
             (display i)
             (newline)
             (do-iter (+ i 1)))))
   (do-iter 0)))

(let ((x '(1 3 5 7 9)))
  ((lambda ()
     (define (do-iter x sum)
       (cond ((null? x) sum)
             (else
               (do-iter (cdr x) (+ sum (car x))))))
     (do-iter x 0))))
```

Notice here that `do-iter` is wrapped in `lambda` to prevent duplicate names.

### implementation

Modify the test based on  [mceval.scm](./mceval.scm) .`eval` adds judgment

``` Scheme
((do? exp) (eval (do->combination exp) env))
```

Implementation code
 
``` Scheme
(define (do? exp) (tagged-list? exp 'do))

(define (do->combination exp)
  (define (do-vars exp) (map car (cadr exp)))
  (define (do-inits exp) (map cadr (cadr exp)))
  (define (do-steps exp) (map caddr (cadr exp)))
  (define (do-command exp) (cdddr exp))
  (define (do-test-exps exp) 
    (define (loop lst)
      (if (null? lst)
          '()
          (cons (list (car lst) (cadr lst)) (loop (cddr lst)))))
    (loop (caddr exp)))
  
  (define (make-iter-else exp)
    (append (cons 'else (do-command exp))
            (list (cons 'do-iter (do-steps exp)))))
  (define (make-iter-cond exp)
    (append (cons 'cond (do-test-exps exp)) 
            (list (make-iter-else exp))))
  (define (make-do-iter exp)
    (list 'define (cons 'do-iter (do-vars exp))
          (make-iter-cond exp)))
  (define (make-let-iter-call exp)
    (list (make-do-iter exp)
          (cons 'do-iter (do-inits exp))))
  (cons (make-lambda '()
                     (make-let-iter-call exp))
        '()))
```        
