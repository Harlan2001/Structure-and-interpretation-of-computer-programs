## exxercise 4.48

Let's add some adjectives.Add a list of adjectives

``` Scheme
(define adjectives '(adj lazy nice shitty))
```

And change `parse-simple-noun-phrase` to

``` Scheme
(define (parse-simple-noun-phrase)
  (amb (list 'simple-noun-phrase
             (parse-word articles)
             (parse-word nouns))
       (list 'simple-noun-phrase
             (parse-word articles)
             (parse-word adjectives)
             (parse-word nouns))))
```

Test sentence

``` Scheme
(parse '(the lazy student with the nice cat sleeps in the class))
```

The output is

```
'(sentence
   (noun-phrase
     (simple-noun-phrase (article the) (adj lazy) (noun student))
     (prep-phrase
       (prep with)
       (simple-noun-phrase (article the) (adj nice) (noun cat))))
   (verb-phrase
     (verb sleeps)
     (prep-phrase (prep in) (simple-noun-phrase (article the) (noun class)))))
```             

### Complete code
```Scheme
#lang sicp

(#%require "ambeval.scm")

(easy-ambeval 10 '(begin

(define (require p)
  (if (not p) (amb)))

(define nouns '(noun student professor cat class))
(define verbs '(verb studies lectures eats sleeps))
(define articles '(article the a))
(define prepositions '(prep for to in by with))
(define adjectives '(adj lazy nice shitty))

(define (parse-sentence)
  (list 'sentence
        (parse-noun-phrase)
        (parse-verb-phrase)))

(define (parse-noun-phrase)
  (define (maybe-extend noun-phrase)
    (amb noun-phrase
         (maybe-extend (list 'noun-phrase
                             noun-phrase
                             (parse-prepositional-phrase)))))
  (maybe-extend (parse-simple-noun-phrase)))

(define (parse-prepositional-phrase)
  (list 'prep-phrase
        (parse-word prepositions)
        (parse-noun-phrase)))

(define (parse-verb-phrase)
  (define (maybe-extend verb-phrase)
    (amb verb-phrase
         (maybe-extend (list 'verb-phrase
                             verb-phrase
                             (parse-prepositional-phrase)))))
  (maybe-extend (parse-word verbs)))

(define (parse-simple-noun-phrase)
  (amb (list 'simple-noun-phrase
             (parse-word articles)
             (parse-word nouns))
       (list 'simple-noun-phrase
             (parse-word articles)
             (parse-word adjectives)
             (parse-word nouns))))

(define (parse-word word-list)
  (require (not (null? *unparsed*)))
  (require (memq (car *unparsed*) (cdr word-list)))
  (let ((found-word (car *unparsed*)))
    (set! *unparsed* (cdr *unparsed*))
    (list (car word-list) found-word)))

(define *unparsed* '())

(define (parse input)
  (set! *unparsed* input)
  (let ((sent (parse-sentence)))
    (require (null? *unparsed*))
    sent))

))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(easy-ambeval 10 '(begin 
                    (parse '(the lazy student with the nice cat sleeps in the class))
                    ))
; '(sentence
;    (noun-phrase
;      (simple-noun-phrase (article the) (adj lazy) (noun student))
;      (prep-phrase
;        (prep with)
;        (simple-noun-phrase (article the) (adj nice) (noun cat))))
;    (verb-phrase
;      (verb sleeps)
;      (prep-phrase (prep in) (simple-noun-phrase (article the) (noun class)))))
```
