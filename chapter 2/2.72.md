## exercise 2.72

### analysis

The `encode-symbol` function of 2.68 is as follows:

``` Scheme
(define (encode-symbol symbol tree)
  (if (leaf? tree)
      (if (eq? symbol (symbol-leaf tree))
          '()
          #f)
      (let ((left-result (encode-symbol symbol (left-branch tree))))
        (if left-result
            (cons 0 left-result)
            (let ((right-result (encode-symbol symbol (right-branch tree))))
              (if right-result
                  (cons 1 right-result)
                  #f))))))
```

The tree for n = 5 in Exercise 2.71 is as follows:

<img src="./exercise_2_71_0.svg">

Note that in our recursive implementation of `encode-symbol`,  we first search for the`left-branch`，and if the left branch can find the symbol, we encode it directly, without searching for the 'right-branch'.

In a tree of n = 5, the least frequent symbol is at the end of the left branch, and the left branch takes precedence. Only n = 5 searches are needed to encode the symbol.

The most frequent symbols are on the right branch and are at the top.The left branch takes precedence, so `encode-symbol` needs to walk through all nodes of the tree to find a matching leaf node and encode the symbol.When n = 1, we have 1 node;When n = 2, there are 3 nodes;When n equals 3, there are 5 nodes.And so on, for n, there are 2 * n - 1 nodes.So the symbol with the most frequent encoding frequency has to be searched 2 * n-1 times.

In conclusion, practice 2.71 tree, encoding the symbol with the most frequent frequency, with a growth rate of 2 * n-1.Encode the least frequent symbol, growing at n.

### verification

To verify the conclusion, you can add some output code based on Exercise 2.69, as follows

``` Scheme
(define (encode-symbol symbol tree)
  ;; Output test information
  (newline)
  (display "encode-symbol " )
  (display (weight tree))
  (if (leaf? tree)
      (if (eq? symbol (symbol-leaf tree))
          '()
          #f)
      (let ((left-result (encode-symbol symbol (left-branch tree))))
        (if left-result
            (cons 0 left-result)
            (let ((right-result (encode-symbol symbol (right-branch tree))))
              (if right-result
                  (cons 1 right-result)
                  #f))))))

;;;;;;;;;;;;;;;;
(define huffman-tree (generate-huffman-tree '((A 1) 
                                              (B 2) 
                                              (C 4) 
                                              (D 8)
                                              (E 16))))
(newline)
(encode '(A) huffman-tree)
(newline)
(encode '(E) huffman-tree)
```
We add some output in `encode-symbol`and print out the weight.Create A frequency tree like the one above, encoding the least frequent symbol 'A', and then the most frequent symbol 'E'.
The output for encoding 'A' is as follows：

```
encode-symbol 31
encode-symbol 15
encode-symbol 7
encode-symbol 3
encode-symbol 1
```
When encoding 'A',  `encode-symbol` is called recursively at the left branch of the tree.n is equal to 5 times.

The output of the code 'E' is as follows:

```
encode-symbol 31
encode-symbol 15
encode-symbol 7
encode-symbol 3
encode-symbol 1
encode-symbol 2
encode-symbol 4
encode-symbol 8
encode-symbol 16
```

So when we encode 'E', we branch left, then right, recursively traversing all the tree nodes.n is equal to 5 times 2 minus 1 is equal to 9 times.
